plugins {
    id "java"
    id "idea"
    id "org.jetbrains.kotlin.jvm"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "net.minecraftforge.gradle.forge"
    id "org.spongepowered.mixin"
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "https://fdpclient.club/" }
    maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
    maven { url = "https://lss233.littleservice.cn/repositories/minecraft/" }
    maven { url = "https://jitpack.io/" }
    maven { url = "https://maven.minecraftforge.net/" }
}

version = "5.0.0"
group = "org.unlegitmc.fdp"
archivesBaseName = "FDPClient"

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = "UTF-8"

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    clientJvmArgs = ["-Dfml.coreMods.load=net.ccbluex.liquidbounce.injection.forge.TransformerLoader", "-Xmx2048m", "-Xms2048m", "-Ddev-mode"]
}

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    include 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    include "com.google.zxing:core:3.5.0"
    include("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        transitive = false
        exclude module: "guava"
        exclude module: "commons-io"
        exclude module: "gson"
        exclude module: "launchwrapper"
        exclude module: "log4j-core"
        exclude module: "slf4j-api"
    }

    annotationProcessor("org.spongepowered:mixin:0.7.11-SNAPSHOT")

    include "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    include "com.jagrosh:DiscordIPC:0.4"

    include("com.github.CCBlueX:Elixir:1.2.6") {
        exclude module: "kotlin-stdlib"
        exclude module: "authlib"
    }

    include("com.github.UnlegitMinecraft:FuckPCL:2.0") {
        exclude module: "kotlin-stdlib-jdk8"
        exclude module: "jna-platform"
    }

    include("com.github.UnlegitMinecraft:Astar3d:bec2291cf2") // pathfinding

    include 'com.github.TheAltening:TheAltening4j:d0771f42d3'
    include 'com.github.TheAltening:API-Java-AuthLib:63a9702615'

    include "org.apache.httpcomponents:httpmime:4.5.14"

    include "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"

    include fileTree(include: ["*.jar"], dir: "libs")
}

shadowJar {
    archiveClassifier.set("")
    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    // Exclusions
    exclude "LICENSE.txt"
    exclude "META-INF/maven/**"
    exclude "META-INF/versions/**"
    exclude "org/apache/log4j/**"
    exclude "org/apache/commons/**"
    exclude "org/junit/**"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    filesMatching("mcmod.info") {
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    rename "(.+_at.cfg)", "META-INF/\$1"
}

task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

moveResources.dependsOn(processResources)
classes.dependsOn(moveResources)

jar {
    manifest.attributes(
            "FMLCorePlugin": "net.ccbluex.liquidbounce.injection.forge.TransformerLoader",
            "FMLCorePluginContainsFMLMod": true,
            "ForceLoadAsMod": true,
            "MixinConfigs": "mixins.fdpclient.json",
            "ModSide": "CLIENT",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": "0",
            "FMLAT": "fdpclient_at.cfg",
    )

    enabled = false
}

mixin {
    disableRefMapWarning = true
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.fdpclient.refmap.json"
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}

tasks.reobfJar.dependsOn(shadowJar)